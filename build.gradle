plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
//  id "com.matthewprenger.cursegradle" version "1.0.9"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = config.mod.version
group = config.mod.group
archivesBaseName = config.mod.name

def getGitRef() {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    return stdout.toString().trim()
  } catch (Throwable e) {
    return "unknown"
  }
}

if (System.getenv("PROMOTED_NUMBER") != null)
    version += ".${System.getenv("PROMOTED_NUMBER")}"
else if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"
else
    version += "+" + getGitRef()

ext.simpleVersion = version
version = "MC${config.minecraft.version}-Rift-${project.version}"

minecraft {
}

repositories {
    mavenCentral()
    ivy {
        artifactPattern "http://asie.pl/javadeps/proto/[module]-[revision](-[classifier]).[ext]"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:18w50a"
    mappings "net.fabricmc:yarn:18w50a.64"
    modCompile "net.fabricmc:fabric-loader:0.3.0.74"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:0.1.2.63"

    compile name: "Jankson", version: "0.0.3"
    compile name: "JanksonShadowed", version: "0.0.3"
    compile 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
    inputs.property "version", project.simpleVersion
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    classifier = 'api'
    include 'li/cil/tis3d/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives javadocJar
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact javadocJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url System.getenv("MAVEN_PATH")
        }
    }
}

//curseforge {
//    apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""
//    project {
//        id = config.curse.project.id
//        releaseType = config.curse.project.releaseType
//        changelogType = "markdown"
//        changelog = file("changelog.md")
//        addGameVersion config.minecraft.version
//        addGameVersion "Java 8"
//
//        relations {
//            requiredLibrary 'fabric'
//        }
//    }
//}

// this is needed for IntelliJ so we don't have to copy over the assets manually every time
idea {
    module {
        outputDir = file('build/classes/main')
    }
}
